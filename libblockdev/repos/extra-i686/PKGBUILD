# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libblockdev
pkgver=2.11
pkgrel=1
pkgdesc="A library for manipulating block devices"
arch=('i686' 'x86_64')
url="https://github.com/rhinstaller/libblockdev"
license=('LGPL')
depends=('btrfs-progs' 'dosfstools' 'dmraid' 'gptfdisk' 'libbytesize' 'lvm2' 'mdadm' 'parted'
         'volume_key' 'xfsprogs' 'kmod')
makedepends=('gobject-introspection' 'systemd' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rhinstaller/libblockdev/archive/$pkgver-1.tar.gz")
sha512sums=('986fc37c69755b262b9a093cd7e9c5be5b8225e2ff37ef4157db63eaa0f47298813971ef09c76d70b5da3881b8cd8f8c8c365a6e9da3fa52e8fc2875d560ab57')

prepare() {
  sed -i 's/-ldmraid //' $pkgname-$pkgver-1/src/lib/Makefile.am
}

build() {
  cd "$srcdir"/$pkgname-$pkgver-1
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --without-lvm_dbus
  make
}

check() {
  cd "$srcdir"/$pkgname-$pkgver-1
  make check
}

package() {
  cd "$srcdir"/$pkgname-$pkgver-1
  make DESTDIR="$pkgdir" install
}
