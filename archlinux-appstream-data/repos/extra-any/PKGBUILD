# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20170909
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="http://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community; do
 source+=($_repo.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-64x64.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz $_repo-icons-{64x64,128x128}.tar.gz)
done
sha256sums=('6c9efa8aa159d1f47244e6811180e255b5c3a9c36b8cfa8241997f20710b8805'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '5644f4b26d21aebdfe3154f4049dbdb678200554d781e542c54e84c6302c9ad3'
            '42e6ad22ff395c2a733d6a579b14030f8adf2fa8850af272c4aa28a6198b6b47'
            '557409852d457a3c2c0e888720dd9f593ca9d187b38ba4a6a88db2d66c0b5264'
            '6c3159de5ace39073897e5684f0a04c3d6a999faf42b56f6b6368567acd17873'
            '62106f67af1d9532a5f707466fff5fa3efa6d9b1866b876e84e97526324c8dbc'
            '6449085f78f983f8b6f7513162df8532b94fc486fb0349fcd2241d569ee1c44f')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
  done
  cp *.xml.gz "$pkgdir"/usr/share/app-info/xmls/
}
