# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-todo
pkgver=3.24.2
pkgrel=1
pkgdesc="Task manager for GNOME"
url="https://wiki.gnome.org/Apps/Todo"
arch=(i686 x86_64)
license=(GPL)
depends=(evolution-data-server libpeas)
makedepends=(intltool gobject-introspection gnome-common appstream-glib git)
optdepends=('python3: Some plugins')
groups=(gnome-extra)
_commit=fc5c2f1a9e646356a6166605c4c5c15f8aae50a6  # tags/GNOME_TODO_3_24_2^0
source=("git+https://git.gnome.org/browse/gnome-todo#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_TODO_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-compile-warnings
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
