# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-2048
pkgver=3.22.0+7+ga460b20
pkgrel=1
pkgdesc="Obtain the 2048 tile"
url="https://wiki.gnome.org/Apps/2048"
arch=(x86_64 i686)
license=(GPL3)
depends=(clutter-gtk libgee libgnome-games-support)
makedepends=(intltool vala itstool yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=a460b20ff476df0f6ea3173a7f30b558708f1ce3  # master
source=("git+https://git.gnome.org/browse/gnome-2048#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
