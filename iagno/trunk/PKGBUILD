# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=iagno
pkgver=3.22.0
pkgrel=1
pkgdesc="Dominate the board in a classic version of Reversi"
url="https://wiki.gnome.org/Apps/Iagno"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 libcanberra librsvg)
makedepends=(intltool itstool gobject-introspection yelp-tools appstream-glib vala git)
groups=(gnome-extra)
_commit=857adea28db86ce5b6235c439409f34588e1da45  # tags/3.22.0^0
source=("git://git.gnome.org/iagno#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
