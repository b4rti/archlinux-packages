# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mines
pkgver=3.24.0+1+ga8c3697
pkgrel=1
pkgdesc="Clear hidden mines from a minefield"
url="https://wiki.gnome.org/Apps/Mines"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 librsvg libgnome-games-support)
makedepends=(intltool itstool gobject-introspection yelp-tools appstream-glib vala git)
groups=(gnome-extra)
_commit=a8c3697a9f5bb7db24222ee22a7eeaf08a2e6277  # gnome-3-24
source=("git+https://git.gnome.org/browse/gnome-mines#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
