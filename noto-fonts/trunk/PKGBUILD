# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=noto-fonts
pkgname=(noto-fonts ttf-croscore)
_pkgver=2017-04-28
_commit=420acaa025c84985ac6b7f457420e15909af1b51  # master
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Google Noto TTF fonts"
arch=(any)
url="https://www.google.com/get/noto/"
license=(custom:SIL)
provides=(ttf-font)
source=("$pkgbase-$_commit.tar.gz::https://github.com/googlei18n/noto-fonts/archive/$_commit.tar.gz"
        66-noto-sans.conf 66-noto-serif.conf 66-noto-mono.conf)
sha256sums=('54def07eae26a2ec98c4ada30f573dc652fe62443b1501b919f447853ec54f0f'
            '65ff237a579d438eb1b1c90bf25b5818e15ab019dbbfed2fc283b9cd12e5177f'
            '27419c0557d05e13edbb244cd06a62d921c271c6c2f07126856519869035776e'
            '8c541eab8b44f6c7b02e981a4fbbd082eb6c1aa8285e11dede5f0a3ec0617772')

package_noto-fonts() {
  optdepends=('noto-fonts-cjk: CJK characters' 'noto-fonts-emoji: Emoji characters')

  cd $pkgbase-*/
  mkdir -p "$pkgdir"/usr/share/fonts/noto
  install -m644 unhinted/Noto*.tt[fc] "$pkgdir"/usr/share/fonts/noto
  install -m644 hinted/Noto*.tt[fc] "$pkgdir"/usr/share/fonts/noto
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/noto-fonts/LICENSE

  # Already in NotoSans-*.ttc
  rm "$pkgdir"/usr/share/fonts/noto/NotoSans{,UI}-*.ttf

  # Install fontconfig files
  mkdir -p "$pkgdir"/etc/fonts/conf.avail
  install -m644 "$srcdir"/*.conf "$pkgdir"/etc/fonts/conf.avail/
}

package_ttf-croscore() {
  pkgdesc='Chrome OS core fonts'

  cd $pkgbase-*/
  mkdir -p "$pkgdir"/usr/share/fonts/TTF
  install -m644 hinted/{Arimo,Cousine,Tinos}*.ttf "$pkgdir"/usr/share/fonts/TTF
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/ttf-croscore/LICENSE
}
