# $Id$
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=hitori
pkgver=3.22.2
pkgrel=1
pkgdesc="GTK+ application to generate and let you play games of Hitori"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Apps/Hitori"
license=(GPL)
depends=(gtk3)
groups=(gnome-extra)
makedepends=(itstool yelp-tools autoconf-archive appstream-glib git)
_commit=f27423093152f3c80255d2d9d84b61f2148c9d81  # tags/3.22.2^0
source=("git+https://git.gnome.org/browse/hitori#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$srcdir/$pkgname"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install
}
