# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=eog-plugins
pkgver=3.16.6
pkgrel=1
pkgdesc="Plugins for Eye of Gnome"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Apps/EyeOfGnome/Plugins"
license=(GPL2)
depends=(eog libchamplain libgdata python)
makedepends=(intltool gobject-introspection gnome-common git)
_commit=29056c63a94bba6426dcc0722df5c3d128384484  # tags/3.16.6^0
source=("git+https://git.gnome.org/browse/eog-plugins#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
