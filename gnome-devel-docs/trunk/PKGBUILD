# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-devel-docs
pkgver=3.22.1
pkgrel=1
pkgdesc="Developer documentation for GNOME"
url="https://git.gnome.org/browse/gnome-devel-docs"
arch=(any)
license=(FDL)
depends=(yelp)
makedepends=(yelp-tools gnome-common git)
groups=(gnome-extra)
_commit=79dd823d8273f28df2db9a5d2cd203306482c42e  # gnome-3-22
source=("git://git.gnome.org/gnome-devel-docs#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  # Fixup missing tag for pkgver()
  git tag -f 3.22.1 79dd823d8273f28df2db9a5d2cd203306482c42e
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
