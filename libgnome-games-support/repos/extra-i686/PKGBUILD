# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libgnome-games-support
pkgver=1.2.2
pkgrel=1
pkgdesc="Code shared between GNOME games"
url="https://git.gnome.org/browse/libgnome-games-support"
arch=(x86_64 i686)
license=(GPL3 LGPL3)
depends=(libgee gtk3)
makedepends=(vala git pkg-config autoconf-archive)
groups=(gnome-extra)
_commit=3d4fb4fde5ef3622fca23143a56487ab2b7c7f06  # tags/1.2.2-really^0
source=("git+https://git.gnome.org/browse/libgnome-games-support#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-really//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
