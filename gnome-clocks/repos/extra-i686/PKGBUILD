# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-clocks
pkgver=3.24.0+4+gd1003cd
pkgrel=1
pkgdesc="Clocks applications for GNOME"
url="https://wiki.gnome.org/Apps/Clocks"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 libcanberra libgweather gnome-desktop geoclue2 geocode-glib gsound)
makedepends=(intltool vala gobject-introspection itstool yelp-tools git)
groups=(gnome-extra)
_commit=d1003cd0e43436e4221fe4d9765364b1aa989c67  # master
source=("git+https://git.gnome.org/browse/gnome-clocks#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
