# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gom
pkgver=0.3.2+12+gc7bd13d
pkgrel=1
pkgdesc="A GObject to SQLite object mapper"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Projects/Gom"
license=(LGPL2.1)
depends=(glib2 sqlite)
makedepends=(intltool gtk-doc gobject-introspection gdk-pixbuf2 git gnome-common)
_commit=c7bd13d64ed05165fdaf6a1efc466128a39f6f3a
source=("git://git.gnome.org/gom#commit=$_commit")
sha256sums=('SKIP')
 
pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
    --disable-static --enable-gtk-doc
  make
}

check() {
  cd $pkgname
  make -k check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
