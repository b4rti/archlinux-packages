# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>

pkgname=vala
_pkgver=0.36
pkgver=${_pkgver}.4
pkgrel=1
pkgdesc='Compiler for the GObject type system'
url='https://wiki.gnome.org/Projects/Vala'
arch=('i686' 'x86_64')
license=('LGPL')
depends=('glib2')
makedepends=('libxslt' 'vala')
checkdepends=('dbus' 'libx11' 'gobject-introspection')
source=(${pkgname}-${pkgver}.tar.xz::https://download.gnome.org/sources/vala/${_pkgver}/vala-${pkgver}.tar.xz)
sha256sums=('e9f23ce711c1a72ce664d10946fbc5953f01b0b7f2a3562e7a01e362d86de059')
sha512sums=('cc804b9ad0bda4968660084c5d5e38761b0d478a6c504d3d2e6188fdd2deef4a4315d7278191f19b3bcbee66e0b02733deb6050d168305c85cef8f203ecc13ac')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-vapigen
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
