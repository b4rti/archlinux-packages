# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-screenshot
pkgver=3.22.0+42+g8472361
pkgrel=1
pkgdesc="Take pictures of your screen"
url="https://git.gnome.org/browse/gnome-screenshot"
arch=(i686 x86_64)
license=(GPL2)
depends=(dconf gtk3 libcanberra)
makedepends=(intltool git)
groups=(gnome)
_commit=8472361f3c23402ffe0367184d497d16984dec0c  # master
source=("git+https://git.gnome.org/browse/gnome-screenshot#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$pkgname"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
