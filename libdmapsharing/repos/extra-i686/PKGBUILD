# $Id$
#Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=libdmapsharing
pkgver=2.9.37+1+ge9fe3e0
pkgrel=1
pkgdesc="A library that implements the DMAP family of protocols"
arch=(i686 x86_64)
url="http://www.flyn.org/projects/libdmapsharing/index.html"
license=(LGPL2.1)
depends=(libsoup avahi gst-plugins-base-libs gdk-pixbuf2)
makedepends=(git gtk-doc gobject-introspection)
_commit=e9fe3e0416e7491eda100f372c7a3cd97183f60b  # master
source=("git+https://git.gnome.org/browse/libdmapsharing#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^LIBDMAPSHARING_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --with-mdns=avahi --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
